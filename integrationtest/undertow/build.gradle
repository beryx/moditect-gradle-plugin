plugins{
    id "java"
    id "application"
    id "org.moditect.gradleplugin"
}

repositories {
    mavenCentral()
    jcenter()
}

group = "org.moditect"
version = "2.0.0"

targetCompatibility = JavaVersion.VERSION_1_9
sourceCompatibility = JavaVersion.VERSION_1_9

ext.moduleName = 'com.example'

mainClassName = 'com.example.HelloWorldServer'
jar {
    manifest {
        attributes("Automatic-Module-Name": moduleName)
    }
}


dependencies{
    compile 'io.undertow:undertow-core:1.4.21.Final'
}

moditect {
    addMainModuleInfo {
        version = project.version
        overwriteExistingFiles = false
        jdepsExtraArgs = ['-q']
        module {
            mainClass = mainClassName
            moduleInfo {
                name = moduleName
                exports = "!*;"
            }
        }
    }
    addDependenciesModuleInfo {
        jdepsExtraArgs = ['-q']
        modules {
            module {
                artifact 'org.jboss.logging:jboss-logging:3.2.1.Final'
                additionalDependency 'org.jboss.logmanager:jboss-logmanager:1.5+'
                additionalDependency 'log4j:log4j:1.2+'
                additionalDependency 'org.slf4j:slf4j-api:1.7+'
                additionalDependency 'org.apache.logging.log4j:log4j-api:2.0+'
                moduleInfo {
                    name = "org.jboss.logging"
                    requires = '''
                        static jboss.logmanager;
                        static log4j*;
                        static slf4j.api;
                        *;
                    '''
                }
            }
            module {
                artifact 'org.jboss.logging:jboss-logging-annotations:1.2.0.Final'
                moduleInfo {
                    name = 'org.jboss.logging.annotations'
                }
            }
            module {
                artifact 'org.jboss.xnio:xnio-api:3.3.8.Final'
                additionalDependency 'org.osgi:org.osgi.core:6+'
                additionalDependency 'org.wildfly.common:wildfly-common:1.2+'
                additionalDependency 'org.wildfly.client:wildfly-client-config:1+'
                additionalDependency 'org.jboss.threads:jboss-threads:2.3+'
                moduleInfo {
                    name = 'org.jboss.xnio.api'
                    addServiceUses = true
                    exports = '''
                        org.xnio._private to org.jboss.logging;
                        *;
                    '''
                }
            }
            module {
                artifact 'org.jboss.xnio:xnio-nio:3.3.8.Final'
                additionalDependency 'org.osgi:org.osgi.core:6+'
                additionalDependency 'org.wildfly.common:wildfly-common:1.2+'
                additionalDependency 'org.wildfly.client:wildfly-client-config:1+'
                additionalDependency 'org.jboss.threads:jboss-threads:2.3+'
                moduleInfo {
                    name = 'org.jboss.xnio.nio'
                    requires = '''
                        static org.jboss.logging.annotations;
                        org.jboss.logging;
                        *;
                    '''
                }
            }
            module {
                artifact 'org.eclipse.jetty.alpn:alpn-api:1.0.0'
                moduleInfo {
                    name = 'alpn.api'
                }
            }
            module {
                artifact 'io.undertow:undertow-core'
                moduleInfo {
                    name = 'io.undertow.core'
                }
            }
        }
    }
    createRuntimeImage {
        modules = ['com.example', 'org.jboss.xnio.nio']
        launcher {
            name = 'helloWorld'
            module = 'com.example'
        }
        compression = 2
        stripDebug = true
    }
}
